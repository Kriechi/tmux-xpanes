#compdef xpanes tmux-xpanes

_xpanes () {
    local opts_omit="-h --help -V --version"
    # (<options which should not exist together>)-option[description]:name:(element1 element2 ...)
    _arguments "(: -)"{-h,--help}"[Show detailed usage.]" \
               "(: -)"{-V,--version}"[Show version.]" \
        "($opts_omit -c --ssh -e)-c[Specify <utility> which is executed as a command in each panes. If <utility> is omitted, echo(1) is used.]" \
        "($opts_omit -e -c -I --ssh)-e[Execute given arguments as is.]" \
        "($opts_omit -I --ssh -e)-I[Replacing one or more occurrences of <repstr> in <utility> given by -c option. Default value of <repstr> is {}.]:repstr:(@ % _REPLACE_ @@@)" \
        "($opts_omit --ssh -I -c -e)--ssh[Let <utility> 'ssh -o StrictHostKeyChecking=no {}'.]" \
        "($opts_omit -S)-S[Specify a full alternative path to the server socket.]:filename:_files" \
        "($opts_omit -l)-l[Specify a layout for a window.]:layout:(eh ev mh mv)" \
        "($opts_omit --log)--log=[Enable logging and store log files to given <directory>.]:dirs:_dir_list" \
        "($opts_omit --log-format)--log-format=[File name of log files follows given <FORMAT>.]" \
        "($opts_omit --kill)--kill=[Close a pane itself after new window is created.]" \
        "($opts_omit --stay)--stay=[Do not switch to new window.]" \
        "($opts_omit -d)"{-d,--desync}"[Make synchronize-panes option off on new window.]"
}

_tmux-xpanes() {
    _xpanes "$@"
}

# Local Variables:
# mode: Shell-Script
# sh-indentation: 4
# indent-tabs-mode: nil
# sh-basic-offset: 4
# End:
# vim: ft=zsh sw=4 ts=4 et
